<!DOCTYPE html>
<html lang="en">
	<head>
		<title>three.js webgl - materials - shaders [custom]</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			body {
				color: #ffffff;
				font-family:Monospace;
				font-size:13px;
				text-align:center;
				font-weight: bold;

				background-color: #050505;
				margin: 0px;
				overflow: hidden;
			}

		

		
		</style>
	</head>
	<body>

		<div id="container"></div>
		

		<script src="three.js"></script>


		<script id="fragment_shader4" type="x-shader/x-fragment">

			uniform float time;
			uniform vec2 resolution;

			varying vec2 vUv;

			void main( void ) {

				vec2 position = -1.0 + 2.0 * vUv;

				float red = abs( sin( position.x * position.y + time / 5.0 ) );
				float green = abs( sin( position.x * position.y + time / 4.0 ) );
				float blue = abs( sin( position.x * position.y + time / 3.0 ) );
				gl_FragColor = vec4( red, green, blue, 1.0 );

			}

		</script>

		<script id="fragment_shader3" type="x-shader/x-fragment">

            #ifdef GL_ES
            precision mediump float;
            #endif

            // Paremeters --------------------------

            const float height = 5.0;
            const float size = 10.0;
            const float noiseSize = 2.0;
            const float noiseStrength = 0.15;
            const int noiseDepth = 3;
            const float speed = 3.0;

            const vec3 color = vec3(2.5, 2.0, 1.5);

            const bool followMouse = false;
            const vec2 defaultPosition = vec2(0.0, -0.8);

            //--------------------------------------


            uniform float time;
            uniform vec2 mouse;
            uniform vec2 resolution;

            varying vec2 vUv;

            // Perlin noise source code from Stefan Gustavson
            // https://github.com/ashima/webgl-noise/blob/master/src/classicnoise2D.glsl

            vec4 mod289(vec4 x)
            {
              return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec4 permute(vec4 x)
            {
              return mod289(((x*34.0)+1.0)*x);
            }

            vec4 taylorInvSqrt(vec4 r)
            {
              return 1.79284291400159 - 0.85373472095314 * r;
            }

            vec2 fade(vec2 t) {
              return t*t*t*(t*(t*6.0-15.0)+10.0);
            }

            // Classic Perlin noise
            float cnoise(vec2 P)
            {
              vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
              vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
              Pi = mod289(Pi); // To avoid truncation effects in permutation
              vec4 ix = Pi.xzxz;
              vec4 iy = Pi.yyww;
              vec4 fx = Pf.xzxz;
              vec4 fy = Pf.yyww;

              vec4 i = permute(permute(ix) + iy);

              vec4 gx = fract(i * (1.0 / 41.0)) * 2.0 - 1.0 ;
              vec4 gy = abs(gx) - 0.5 ;
              vec4 tx = floor(gx + 0.5);
              gx = gx - tx;

              vec2 g00 = vec2(gx.x,gy.x);
              vec2 g10 = vec2(gx.y,gy.y);
              vec2 g01 = vec2(gx.z,gy.z);
              vec2 g11 = vec2(gx.w,gy.w);

              vec4 norm = taylorInvSqrt(vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11)));
              g00 *= norm.x;
              g01 *= norm.y;
              g10 *= norm.z;
              g11 *= norm.w;

              float n00 = dot(g00, vec2(fx.x, fy.x));
              float n10 = dot(g10, vec2(fx.y, fy.y));
              float n01 = dot(g01, vec2(fx.z, fy.z));
              float n11 = dot(g11, vec2(fx.w, fy.w));

              vec2 fade_xy = fade(Pf.xy);
              vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
              float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
              return 2.3 * n_xy;
            }


            void main( void ) {
                vec2 position = (followMouse ? mouse : defaultPosition);
                
                vec2 fragment =  -1.0 + 2.0 * vUv;
                // Compute flame area
                vec2 coord;
                if (fragment.y > position.y) {
                    coord = vec2(fragment.x, position.y + (fragment.y - position.y) / height);
                } else {
                    coord = fragment.xy;
                }
                float dist = distance(position, coord);
                
                // Compute flame noise
                vec2 noisePosition = noiseSize * (fragment.xy - position) - vec2(0.0, speed * time);
                float noise = 0.0;
                for (int i = 0; i < noiseDepth; i++) {
                    noise += cnoise(noisePosition * pow(2.0, float(i)));
                }
                
                gl_FragColor = vec4(mix(-size * dist, noise, noiseStrength) + color, 1.0);
                }

		</script>

		<script id="fragment_shader2" type="x-shader/x-fragment">       //有纹理的

			uniform float time;
			uniform vec2 resolution;

			uniform sampler2D texture;

			varying vec2 vUv;

			void main( void ) {

				vec2 position = -1.0 + 2.0 * vUv;

				float a = atan( position.y, position.x );
				float r = sqrt( dot( position, position ) );

				vec2 uv;
				uv.x = cos( a ) / r;    //position.x/r^2
				uv.y = sin( a ) / r;    //position.y/r^2
				uv /= 10.0;
				uv += time * 0.05;

				vec3 color = texture2D( texture, uv ).rgb;

				gl_FragColor = vec4( color * r * 1.5, 1.0 );

			}
		</script>

		<script id="fragment_shader1" type="x-shader/x-fragment">

			uniform vec2 resolution;
			uniform float time;

			varying vec2 vUv;

			void main(void)
			{

				vec2 p = -1.0 + 2.0 * vUv;
				float a = time*40.0;
				float d,e,f,g=1.0/40.0,h,i,r,q;

				e=400.0*(p.x*0.5+0.5);
				f=400.0*(p.y*0.5+0.5);
				i=200.0+sin(e*g+a/150.0)*20.0;
				d=200.0+cos(f*g/2.0)*18.0+cos(e*g)*7.0;
				r=sqrt(pow(i-e,2.0)+pow(d-f,2.0));
				q=f/r;
				e=(r*cos(q))-a/2.0;f=(r*sin(q))-a/2.0;
				d=sin(e*g)*176.0+sin(e*g)*164.0+r;
				h=((f+d)+a/2.0)*g;
				i=cos(h+r*p.x/1.3)*(e+e+a)+cos(q*g*6.0)*(r+h/3.0);
				h=sin(f*g)*144.0-sin(e*g)*212.0*p.x;
				h=(h+(f-e)*q+sin(r-(a+h)/7.0)*10.0+i/4.0)*g;
				i+=cos(h*2.3*sin(a/350.0-q))*184.0*sin(q-(r*4.3+a/12.0)*g)+tan(r*g+h)*184.0*cos(r*g+h);
				i=mod(i/5.6,256.0)/64.0;
				if(i<0.0) i+=4.0;
				if(i>=2.0) i=4.0-i;
				d=r/350.0;
				d+=sin(d*d*8.0)*0.52;
				f=(sin(a*g)+1.0)/2.0;
				gl_FragColor=vec4(vec3(f*i/1.6,i/2.0+d/13.0,i)*d*p.x+vec3(i/1.3+d/8.0,i/2.0+d/18.0,i)*d*(1.0-p.x),1.0);

			}

		</script>

		<script id="vertexShader" type="x-shader/x-vertex">

			varying vec2 vUv;

			void main()
			{
				vUv = uv;
				vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
				gl_Position = projectionMatrix * mvPosition;
			}

		</script>

		<script>

		

			var container, stats;

			var start_time;

			var camera, scene, renderer;

			var uniforms1, uniforms2, material1, material2, mesh, meshes = [];

			var mouseX = 0, mouseY = 0,
			lat = 0, lon = 0, phy = 0, theta = 0;

			var windowHalfX = window.innerWidth / 2;
			var windowHalfY = window.innerHeight / 2;

			var clock = new THREE.Clock();

			init();
			animate();

			function init() {

				container = document.getElementById( 'container' );

				camera = new THREE.PerspectiveCamera( 40, windowHalfX / windowHalfY, 1, 3000 );
				camera.position.z = 4;

				scene = new THREE.Scene();

				start_time = Date.now();

				uniforms1 = {
					time: { type: "f", value: 1.0 },
					resolution: { type: "v2", value: new THREE.Vector2() }
				};

				uniforms2 = {
					time: { type: "f", value: 1.0 },
					resolution: { type: "v2", value: new THREE.Vector2() },
					texture: { type: "t", value: THREE.ImageUtils.loadTexture( "lavatile.jpg" ) }
				};

				uniforms2.texture.value.wrapS = uniforms2.texture.value.wrapT = THREE.RepeatWrapping;

				var size = 0.75, mlib = [],
					params = [ [ 'fragment_shader1', uniforms1 ],  [ 'fragment_shader2', uniforms2 ], [ 'fragment_shader3', uniforms1 ], [ 'fragment_shader4', uniforms1 ] ];

				for( var i = 0; i < params.length; i++ ) {

					material = new THREE.ShaderMaterial( {

						uniforms: params[ i ][ 1 ],
						vertexShader: document.getElementById( 'vertexShader' ).textContent,
						fragmentShader: document.getElementById( params[ i ][ 0 ] ).textContent

						} );

					mlib[ i ] = material;

					mesh = new THREE.Mesh( new THREE.CubeGeometry( size, size, size ), new THREE.MeshFaceMaterial( [ mlib[ i ], mlib[ i ], mlib[ i ], mlib[ i ], mlib[ i ], mlib[ i ] ] ) );
					mesh.position.x = i - ( params.length - 1 ) / 2;
					mesh.position.y = i % 2 - 0.5;
					scene.add( mesh );

					meshes[ i ] = mesh;

				}

				renderer = new THREE.WebGLRenderer();
				container.appendChild( renderer.domElement );


				onWindowResize();

				window.addEventListener( 'resize', onWindowResize, false );

			}

			function onWindowResize( event ) {

				uniforms1.resolution.value.x = window.innerWidth;
				uniforms1.resolution.value.y = window.innerHeight;

				uniforms2.resolution.value.x = window.innerWidth;
				uniforms2.resolution.value.y = window.innerHeight;

				camera.aspect = window.innerWidth / window.innerHeight;
				camera.updateProjectionMatrix();

				renderer.setSize( window.innerWidth, window.innerHeight );

			}

			//

			function animate() {

				requestAnimationFrame( animate );

				render();

			}

			function render() {

				var delta = clock.getDelta();     //距离上次绘制经过的时间（秒）

				uniforms1.time.value += delta * 5;
				uniforms2.time.value = clock.elapsedTime;    //clock第一次调用getDelta后经过的时间（秒）

				for( var i = 0; i < meshes.length; ++ i ) {

			//		meshes[ i ].rotation.y += delta * 0.5 * ( i % 2 ? 1 : -1 );
					meshes[ i ].rotation.x += delta * 0.5 * ( i % 2 ? -1 : 1 );

				}

				renderer.render( scene, camera );

			}

		</script>

	</body>
</html>
